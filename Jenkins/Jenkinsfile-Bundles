//

pipeline {

  agent {
    node {
      label "unity3d-2019.3-vulkan"
      customWorkspace "/home/jenkins/workspace/simulator-2019.3"
    }
  }

  options {
    gitLabConnection("${GITLAB_HOST}")
    skipDefaultCheckout(true)
    buildDiscarder(logRotator(numToKeepStr: '20'))
    timestamps()
  }

  triggers {
    gitlab(triggerOnPush: true, triggerOnMergeRequest: true, branchFilterType: "NameBasedFilter", includeBranchesSpec: "master", excludeBranchesSpec: "")
  }

  parameters {
    string(name: "CLOUD_URL", defaultValue: "${SIMULATOR_STAGING_CLOUD_URL}", description: "")
    booleanParam(name: "FORCE_REBUILD", defaultValue: false, description: "")
    booleanParam(name: "UPLOAD", defaultValue: true, description: "Upload bundles to S3")
    string(name: "BUILD_SIMULATOR_ENVIRONMENTS", defaultValue: "${SIMULATOR_ENVIRONMENTS}", description: "Environments to build")
    string(name: "BUILD_SIMULATOR_VEHICLES", defaultValue: "${SIMULATOR_VEHICLES}", description: "Vehicles to build")
  }

  environment {
    UNITY_USERNAME = credentials("UNITY_USERNAME")
    UNITY_PASSWORD = credentials("UNITY_PASSWORD")
    UNITY_SERIAL = credentials("UNITY_SERIAL")
    PYTHONUNBUFFERED = "1"
    DISPLAY = ":0"
    JENKINS_BUILD_ID = "${BUILD_ID}"
    GIT_COMMIT = sh(returnStdout: true, script: "git rev-parse HEAD").trim()
  }

  stages {
    stage("Checkout Simulator") {
        steps {
            checkout scm
            sh """
                git lfs install
                git lfs pull origin
            """
            sh "rm -rf *.zip *.html *.xml *.log.gz *.log *.txt"
        }
    }

    stage("Checkout Environments") {
      when {
        not {
            environment name: "BUILD_SIMULATOR_ENVIRONMENTS", value: ""
        }
      }
      steps {
        script {

          def environments = env.BUILD_SIMULATOR_ENVIRONMENTS.split(',')
          for (int i=0; i<environments.size(); i++) {
            def environment = environments[i]
            checkout([
              $class: "GitSCM",
              branches: [[name: "refs/heads/master"]],
              browser: [$class: "GitLab", repoUrl: "https://${GITLAB_HOST}/HDRP/Environments/${environment}", version: env.GITLAB_VERSION],
              extensions: [
                [$class: "RelativeTargetDirectory", relativeTargetDir: "Assets/External/Environments/${environment}"],
                [$class: "LocalBranch"],
                [$class: "GitLFSPull"]
              ],
              userRemoteConfigs: [[
                credentialsId: "auto-gitlab",
                url: "git@${GITLAB_HOST}:HDRP/Environments/${environment}.git"
              ]]
            ])
          }

          env.SIM_ENVIRONMENTS = sh(returnStdout: true, script: "./Jenkins/get-assets.sh Assets/External/Environments ${S3_DOWNLOAD_HOST} environment 0 ${BUILD_SIMULATOR_ENVIRONMENTS}").trim()
        } // script
        sh "printenv SIM_ENVIRONMENTS | tee environments.txt"
      }

      post {
        success {
            archiveArtifacts "environments.txt"
        }
      }
    }

    stage("Checkout Vehicles") {
      when {
        not {
            environment name: "BUILD_SIMULATOR_VEHICLES", value: ""
        }
      }
      steps {
        script {
          def vehicles = env.BUILD_SIMULATOR_VEHICLES.split(',')
          for (int i=0; i<vehicles.size(); i++) {
            def vehicle = vehicles[i]
            checkout([
              $class: "GitSCM",
              branches: [[name: "refs/heads/master"]],
              browser: [$class: "GitLab", repoUrl: "https://${GITLAB_HOST}/HDRP/Vehicles/${vehicle}", version: env.GITLAB_VERSION],
              extensions: [
                [$class: "RelativeTargetDirectory", relativeTargetDir: "Assets/External/Vehicles/${vehicle}"],
                [$class: "LocalBranch"],
                [$class: "GitLFSPull"]
              ],
              userRemoteConfigs: [[
                credentialsId: "auto-gitlab",
                url: "git@${GITLAB_HOST}:HDRP/Vehicles/${vehicle}.git"
              ]]
            ])
          }

          env.SIM_VEHICLES = sh(returnStdout: true, script: "./Jenkins/get-assets.sh Assets/External/Vehicles ${S3_DOWNLOAD_HOST} vehicle 0 ${BUILD_SIMULATOR_VEHICLES}").trim()
        } // script

        sh "printenv SIM_VEHICLES | tee vehicles.txt"
      }
      post {
        success {
            archiveArtifacts "vehicles.txt"
        }
      }
    }

    stage("Docker") {
      environment {
        DOCKER = credentials("Jenkins-Gitlab")
      }
      steps {
        dir("Jenkins") {
          sh """
            docker login -u ${DOCKER_USR} -p ${DOCKER_PSW} ${GITLAB_HOST}:4567
            docker-compose build build-simulator
            docker-compose push build-simulator
          """
        }
      }
    }

    stage("Build") {
      when {
        anyOf {
          not {
            environment name: "SIM_ENVIRONMENTS", value: ""
          }
          not {
            environment name: "SIM_VEHICLES", value: ""
          }
        }
      }
      steps {
        dir("Jenkins") {
          sh "UID=`id -u` docker-compose run --rm build-bundles | tee ../build-bundles-${JENKINS_BUILD_ID}.log"
          sh "./check-unity-log.sh ../build-bundles-${JENKINS_BUILD_ID}.log"
        }
      }
      post {
        always {
            sh "gzip *.log"
            archiveArtifacts "*.log.gz"
        }
      }
    } // Build

    stage("Upload") {
      when {
        environment name: "UPLOAD", value: "true"
        anyOf {
          not {
            environment name: "SIM_ENVIRONMENTS", value: ""
          }
          not {
            environment name: "SIM_VEHICLES", value: ""
          }
        }
      }
      steps {
        dir("Jenkins") {
          sh "UID=`id -u` docker-compose run --rm upload-bundles  | tee ../upload-bundles-${JENKINS_BUILD_ID}.log"
        }
      }
      post {
        always {
            sh "gzip *.log"
            archiveArtifacts "*.log.gz"
        }
      }
    } // Upload

  } // stages

  post {
    failure {
      updateGitlabCommitStatus state: "failed"
    }
    success {
      updateGitlabCommitStatus state: "success"
    }

  }

}
